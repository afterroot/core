/*
 * Copyright (C) 2020 Sandip Vaghela
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *          http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group 'com.afterroot.core'

project.ext {
    mavGitUrl = 'https://github.com/sandipv22/Core.git'
    mavLibraryDescription = "Core Package for AfterROOT Apps"
    mavLibraryLicenses = ["Apache-2.0": 'http://www.apache.org/licenses/LICENSE-2.0.txt']
    mavProjectName = 'Core'
    mavPublishToMavenLocal = true
    mavSiteUrl = 'https://afterroot.web.app'
}

def properties = new Properties()
properties.load(new FileInputStream("local.properties"))

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/core-release.aar")
            groupId
            artifactId 'core'
            version "${version}"

            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'Core'
        description = "Core Package for AfterROOT Apps"
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sandipv22/Core.git'
        version {
            name = "${version}"
            desc = "Version ${version}"
            released = new Date()
            vcsTag = "${version}"
        }
    }
}

apply from: 'https://raw.githubusercontent.com/sky-uk/gradle-maven-plugin/master/gradle-mavenizer.gradle'